import gov.ca.cwds.rest.exception.IssueDetails;
import gov.ca.cwds.rest.exception.IssueType;
import gov.ca.cwds.cics.model.AddressData;
import org.drools.core.util.StringUtils;

global java.util.Set validationDetailsList

rule "address-R-00725"

  @doc_description("R - 00725 Street Name exigency") 
  @doc_external_documentation("docTool")
  @doc_enforcement_location("FE, BFE, ZOS")
  @doc_rule_type("DocTool")
  @doc_drools_file("address-R-00725.drl")
  @doc_rule_name("ADDRESS-R-00725")
  
  @doc_doctool_name("R - 00725 Street Name exigency")
  @doc_doctool_description("If Street Number is entered, then Street Name is mandatory")
  @doc_doctool_access_logic("If ADDRESS.Street Number not = null, then ADDRESS.Street Name must not be blank")

  dialect "mvel"
  agenda-group "AddressBusinessRules-validation-agenda"
  
  when
    addressData: AddressData(!StringUtils.isEmpty(streetNumber) && StringUtils.isEmpty(streetName))
  then
    IssueDetails details = new IssueDetails();
    details.setUserMessage(rule.getMetaData().get("doc_description") + ", address: " + addressData.identifier);
    details.setCode(drools.getRule().getName());
    details.setType(IssueType.BUSINESS_VALIDATION);
    details.setProperty("addressData.streetName");
    details.setInvalidValue(addressData.streetName);
    validationDetailsList.add(details);
end
