import gov.ca.cwds.rest.exception.IssueDetails
import gov.ca.cwds.data.legacy.cms.entity.Client
import gov.ca.cwds.cms.data.access.dto.ClientEntityAwareDTO;

global java.util.Set<IssueDetails> validationDetailsList

rule "R-00741"
  @rule_type("VALIDATION")  
  @rule_description("The primary and secondary languages of a client may not be the same.")

  dialect "mvel"
  agenda-group "CalsClientBusinessRules-validation-agenda"
  when
      /*
        Code: R - 00741

        Rule Txt:
        The primary and secondary languages of a client may not be the same.

        Logic:
        CLIENT.Primary Language Type <> CLIENT.Secondary Language Type
      */
      $clientAwareDTO: ClientEntityAwareDTO()
      $client: Client(secondaryLanguageCode > (short)0, primaryLanguageCode == secondaryLanguageCode ) from $clientAwareDTO.entity
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00741");
      details.setUserMessage(String.format("The primary and secondary languages of a client may not be the same. "
      + "Client ID:'%s', Language Code: %d", $client.getIdentifier(), $client.getPrimaryLanguageCode()));
      details.setTechnicalMessage("Client primary and secondary languages codes may not be equals and greater than 0");
      validationDetailsList.add(details);
end
