import gov.ca.cwds.rest.exception.IssueDetails;
import gov.ca.cwds.data.legacy.cms.entity.Client;
import gov.ca.cwds.data.legacy.cms.entity.HealthReferral;
import gov.ca.cwds.cms.data.access.dto.ChildClientEntityAwareDTO

global java.util.Set validationDetailsList

rule "R-10336"
  @rule_type("VALIDATION")  
  @rule_description("The Consent on File Date cannot be less than the birth date (if it exists) of the Child Client.")

  dialect "mvel"
  agenda-group "CalsClientBusinessRules-validation-agenda"
  when
      /*
        Code: R - 10336

        Rule Txt:
        The Consent on File Date cannot be less than the birth date (if it exists) of the Child Client.

        Logic:
        HEALTH_REFERRAL.Consent_On_File_Date must be >= CLIENT.Birth_Date.
      */
      $clientAwareDTO: ChildClientEntityAwareDTO()
      $child: Client($birthDate: birthDate != null) from $clientAwareDTO.entity
      $healthReferral: HealthReferral(consentOnFileDate != null, consentOnFileDate.isBefore($birthDate)) from $clientAwareDTO.healthReferrals
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-10336");
      details.setUserMessage(String.format("The Consent on File Date cannot be less than the birth date (if it exists) of the Child Client."
      + " Child Client ID:'%s' , Child birth date: %tm/%<td/%<tY, Health Referral ID:'%s', Health Referral ConsentOnFile Date: %tm/%<td/%<tY",
      $child.getIdentifier(), $child.getBirthDate(), $healthReferral.getId(), $healthReferral.getConsentOnFileDate()));
      details.setTechnicalMessage("If Client.birthDate != null then for each HealthReferral should be: "
      + "if HealthReferral.consentOnFileDate != null then HealthReferral.consentOnFileDate >= Client.birthDate");
      validationDetailsList.add(details);
end